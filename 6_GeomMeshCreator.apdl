!*******************************************************************************
!				Построение геометрии и сетки
!*******************************************************************************
SELTOL,1e-8
BTOL,1e-7

! линии для пологой оболочки
K,1,0,Chord/2,0
K,2,0,0,Rise
K,3,0,-Chord/2,0

K,100,0,0,Rise-Radius
K,101,0,Radius,Rise-Radius
CSKP, 11, 1, 100, 2, 101, 1
CSYS,11
L,1,3
*GET,NUM_L_DRAG,LINE,0,NUM,MAX

!оболочка
TYPE,1
K,1001,Radius,0,0	!КП ДЛЯ ЛИНИИ ВЫДАВЛИВАНИЯ
K,1002,Radius,0,Length	!КП ДЛЯ ЛИНИИ ВЫДАВЛИВАНИЯ
L,1001,1002
LSEL,S,LOC,Z,Length/2
*GET,NUM_L_PATH,LINE,0,NUM,MAX
ALLS
ADRAG,NUM_L_DRAG,,,,,,NUM_L_PATH

!НАНЕСЕНИЕ AREA ДЛЯ ПЬЕЗО 
*AFUN,DEG
PIEZO_STEP_X = Piezo_size_X + Piezo_X_gap
LENGTH_INC = Piezo_X_offset + Piezo_size_X/2
PIEZO_STEP_Y = Piezo_angle_width + Piezo_angle_gap
ANGLE_INC = -Alpha/2 + Piezo_angle_offset + Piezo_angle_width/2

*DO,I,1,Num_of_piezo_X	
	*DO,J,1,Num_of_piezo_A
		CSYS,11
		CLOCAL,20,1,Radius-Length/2,ANGLE_INC,LENGTH_INC
		WPCSYS,20
		WPROTA,0,0,90
		BLC4,-Piezo_size_X/2,-Piezo_blc_size_Y/2,Piezo_size_X,Piezo_blc_size_Y,Length
		
		VDELE,ALL		
		AOVLAP,ALL
		WPCSYS,0
		CSYS,11	
		ALLS
		!при таком подходе пластина получается нерегулярной
		ASEL,U,LOC,X,Radius		
		ADELE,ALL,,,1	
		ALLS
		
		ANGLE_INC = ANGLE_INC + PIEZO_STEP_Y
	*ENDDO	
	LENGTH_INC = LENGTH_INC + PIEZO_STEP_X	
	ANGLE_INC = -Alpha/2 + Piezo_angle_offset + Piezo_angle_width/2
*ENDDO

*IF,Piezo_round_mirror_FLAG,EQ,1,THEN	
	LENGTH_INC = Piezo_X_offset + Piezo_size_X/2	
	ANGLE_INC = Alpha/2 - Piezo_angle_offset - Piezo_angle_width/2

	*DO,I,1,Num_of_piezo_X	
		*DO,J,1,Num_of_piezo_A
			CSYS,11
			CLOCAL,20,1,Radius-Length/2,ANGLE_INC,LENGTH_INC
			WPCSYS,20
			WPROTA,0,0,90
			BLC4,-Piezo_size_X/2,-Piezo_blc_size_Y/2,Piezo_size_X,Piezo_blc_size_Y,Length
			
			VDELE,ALL		
			AOVLAP,ALL
			WPCSYS,0
			CSYS,11	
			ALLS
			!при таком подходе пластина получается нерегулярной
			ASEL,U,LOC,X,Radius		
			ADELE,ALL,,,1	
			ALLS
			
			ANGLE_INC = ANGLE_INC - PIEZO_STEP_Y
		*ENDDO	
		LENGTH_INC = LENGTH_INC + PIEZO_STEP_X	
		ANGLE_INC = Alpha/2 - Piezo_angle_offset - Piezo_angle_width/2
	*ENDDO	
*ENDIF

!НАНЕСЕНИЕ СЕТКИ
!если сетка несовместная, удаляется выколатая область и строится начальная
*IF,Mesh_opt_FLAG,EQ,0,THEN
!УДАЛЕНИЕ ВЫКОЛОТОЙ ПЛАСТИНЫ
	ALLS
	CSYS,11
	KSEL,S,LOC,X,Radius,
	KSEL,R,LOC,Y,-Alpha/2
	KSEL,R,LOC,Z,0
	LSLK,S,0
	ASLL,S,0
	ADELE,ALL,,,1
	
!ПОСТРОЕНИЕ ОБОЛОЧКИ ЗАНОВО
	CSYS,0
	K,1,0,Chord/2,0
	K,2,0,0,Rise
	K,3,0,-Chord/2,0
	K,100,0,0,Rise-Radius
	K,101,0,Radius,Rise-Radius
	CSYS,11
	L,1,3
	!оболочка
	TYPE,1
	K,1001,Radius,0,0	!КП ДЛЯ ЛИНИИ ВЫДАВЛИВАНИЯ
	K,1002,Radius,0,Length	!КП ДЛЯ ЛИНИИ ВЫДАВЛИВАНИЯ
	L,1001,1002
	LSEL,S,LOC,Z,0	
	ALLS
	ADRAG,NUM_L_DRAG,,,,,,NUM_L_PATH

!НАНЕСЕНИЕ СЕТКИ НА ОБОЛОЧКУ
	TYPE,1	
	ESIZE,Shell_elem_size
	AMESH,1
	
!КОНТАКТЫ НА ОБОЛОЧКУ	
	TYPE,4
	AREVERSE,1
	ESURF,,TOP

!ПЬЕЗО СЕТКА
	TYPE,5
	ALLS
	ESIZE,Piezo_elem_size
	ASEL,U,,,1
	AMESH,ALL
	TYPE,2
	MAT,2
	CSYS,11
	ESIZE,,Piezo_Z_div
	VEXT,ALL,,,Piezo_size_Z

!КОНТАКТ НА ПЬЕЗО	
	ASEL,S,LOC,X,Radius
	ASEL,U,,,1
	ACLEAR,ALL
	TYPE,3	
	AMESH,ALL
	ALLS
	
*ELSE	!СОВМЕСТНАЯ СЕТКА
	!разбиение оболочки
	TYPE,1
	MAT,1

	ALLS
	ESIZE,Shell_elem_size
	AMESH,ALL

	!выдавливание пьезо
	TYPE,2
	MAT,2
	CSYS,11
	KSEL,S,LOC,X,Radius,
	KSEL,R,LOC,Y,-Alpha/2
	KSEL,R,LOC,Z,0
	LSLK,S,0
	ASLL,U,0
	ESIZE,,Piezo_Z_div
	VEXT,ALL,,,Piezo_size_Z
*ENDIF